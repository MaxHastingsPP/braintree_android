plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
}

android {
    namespace = 'com.braintreepayments.api.paypalnativecheckout'
    compileSdkVersion rootProject.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            all {
                jvmArgs '-noverify'
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}

dependencies {
    api project(':BraintreeCore')

    implementation deps.appCompat
    implementation project(':PayPalDataCollector')

    implementation('com.paypal.checkout:android-sdk:0.8.2') {
        exclude group: 'com.paypal.android.sdk', module: 'data-collector'
    }

    testImplementation deps.robolectric
    testImplementation deps.powermockJunit
    testImplementation deps.powermockRule
    testImplementation deps.powermockMockito
    testImplementation deps.powermockClassloading
    testImplementation deps.jsonAssert
    testImplementation deps.mockitoCore
    testImplementation project(':TestUtils')
}

// region signing and publishing

task javadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
}

task javadocsJar(type: Jar, dependsOn: javadocs) {
    archiveClassifier.set('javadoc')
    from javadocs.destinationDir
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

signing {
    required { !version.endsWith("SNAPSHOT") }
    sign publishing.publications
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = 'com.braintreepayments.api'
            artifactId = 'paypal-native-checkout'
            version = rootProject.versionName

            artifact sourcesJar
            artifact javadocsJar

            pom {
                name = 'paypal-native-checkout'
                packaging = 'aar'
                description = 'PayPal Native Checkout dependencies for Braintree Android.'
                url = 'https://github.com/braintree/braintree_android'

                scm {
                    url = 'scm:git@github.com:braintree/braintree_android.git'
                    connection = 'scm:git@github.com:braintree/braintree_android.git'
                    developerConnection = 'scm:git@github.com:braintree/braintree_android.git'
                }

                developers {
                    developer {
                        id = 'devs'
                        name = 'Braintree Payments'
                    }
                }

                licenses {
                    license {
                        name = 'MIT'
                        url = 'http://opensource.org/licenses/MIT'
                        distribution = 'repo'
                    }
                }
            }

            afterEvaluate {
                from components.release
            }
        }
    }
}

// endregion
